# >>> Zcompile define >>>
function source {
  ensure_zcompiled $1
  builtin source $1
}

function ensure_zcompiled {
  local compiled="$1.zwc"
  if [[ ! -r "$compiled" || "$1" -nt "$compiled" ]]; then
    zcompile $1
  fi
}

ensure_zcompiled ~/.zshrc
# <<< Zcompile define >>>

# >>> Sheldon initialize >>>
ZSH_CONFIG_DIR="${HOME}/.config/zsh"
export SHELDON_CONFIG_DIR="${ZSH_CONFIG_DIR}/sheldon"
if [[ -e "${SHELDON_CONFIG_DIR}" ]]; then
  sheldon_bin="${HOME}/.local/bin"
  sheldon_cache="${SHELDON_CONFIG_DIR}/sheldon.zsh"
  sheldon_toml="${SHELDON_CONFIG_DIR}/plugins.toml"
  if [[ ! -r "${sheldon_cache}" || "${sheldon_toml}" -nt "${sheldon_cache}" ]]; then
    rm -f ${sheldon_cache}
    ${sheldon_bin}/sheldon source >${sheldon_cache}
  fi
  source "${sheldon_cache}"
  unset sheldon_bin sheldon_cache sheldon_toml
fi
# <<< Sheldon initialize <<<

# >>> Zprof execute >>>
# if (which zprof > /dev/null 2>&1) ; then
#   zprof | less
# fi
# <<< Zprof execute <<<

zsh-defer unfunction source
